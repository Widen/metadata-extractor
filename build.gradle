apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'idea'

group = 'widen'
version = '3.0.0-SNAPSHOT'

jar {
    manifest {
        attributes 'Implementation-Title': 'metadata-extractor',
                   'Implementation-Version': version,
                   'Main-Class': 'com.drew.imaging.ImageMetadataReader'
    }
}

sourceSets {
    main {
        java.srcDirs = ['Source']
        resources.srcDirs = ['Resources']
    }
    test {
        java.srcDirs = ["Tests"]
    }
}

compileJava {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    options.encoding = 'UTF-8'
}

dependencies {
    compile 'com.adobe.xmp:xmpcore:5.1.3'
    testCompile 'junit:junit:4.12'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

repositories {
    maven {
        credentials {
            username System.env.JFROG_USER ?: USER
            password System.env.JFROG_PASS ?: PASSWORD
        }
        url 'https://widen.jfrog.io/widen/repo/'
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username System.env.JFROG_USER ?: USER
                password System.env.JFROG_PASS ?: PASSWORD
            }

            if (project.version.endsWith("-SNAPSHOT")) {
                url 'https://widen.jfrog.io/widen/libs-snapshots-local'
            }
            else {
                url 'https://widen.jfrog.io/widen/libs-releases-local'
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

tasks.idea {
    dependsOn = [ cleanIdeaModule, ideaModule ]
}

idea {
    module {
        iml {
            beforeMerged { module ->
                module.dependencies.clear()
            }
        }
    }
}
